config {
    type: "incremental",
    schema: dataform.projectConfig.vars.user_event_dataset,
    description: "This table will be imported to Retail API daily",
    tags: ["user_event"]
}

  -- Retail API has strict table tschema requirements, for example visitorId column must be Required and STRING,
  -- type: "table" will overwrite table schema, which converts Required columns to Nullable, such changes will cause import failed
  -- and there's no fast way to set the column to Required;
  -- therefore, everytime this query runs, DROP TABLE -> CREATE TABLE -> APPEND to avoid schema overwrite
SELECT
  "home-page-view" AS eventType,
  visitorId,
  CAST(FORMAT_TIMESTAMP("%Y-%m-%dT%I:%M:%SZ",eventTime) AS STRING) AS eventTime,
  STRUCT(userInfo_userId AS userId) AS userInfo
FROM
  ${ref('action_*')}
WHERE
  _TABLE_SUFFIX = FORMAT_DATE('%Y%m%d', DATE_SUB(CURRENT_DATE("Asia/Taipei"), INTERVAL 1 DAY))
  AND eventType = "home"
  AND visitorId IS NOT NULL
  AND DATE(eventTime) = DATE_SUB(CURRENT_DATE("Asia/Taipei"), INTERVAL 1 DAY)

pre_operations {
  DROP TABLE IF EXISTS
    ${self()}

  ---

  CREATE TABLE
    ${self()} ( eventType STRING NOT NULL,
      visitorId STRING NOT NULL,
      eventTime STRING NOT NULL,
      userInfo STRUCT<userId STRING>)
}
