config {
    type: "table",
    schema: dataform.projectConfig.vars.product_catalog_dataset,
    description: "Product with rating scores",
    tags: ["product"]
}

WITH
  -- Flatten ratingHistogram ARRAY AND multiply score FOR later calculation
  rating_score AS (
  SELECT
    id,
    ( 
    rating.ratingHistogram[OFFSET(0)] * 1 + 
    rating.ratingHistogram[OFFSET(1)] * 2 + 
    rating.ratingHistogram[OFFSET(2)] * 3 + 
    rating.ratingHistogram[OFFSET(3)] * 4 + 
    rating.ratingHistogram[OFFSET(4)] * 5 ) AS total_score,
    SUM(rating_counts) AS ratingCount
  FROM
    ${ref('raw_product_catalog_*')},
    UNNEST(rating.ratingHistogram) AS rating_counts
  WHERE
    _TABLE_SUFFIX = FORMAT_DATE('%Y%m%d', DATE_SUB(CURRENT_DATE("Asia/Taipei"), INTERVAL 1 DAY))
  GROUP BY
    1,
    2 ),
  -- Count total reviews AND compute average rating FOR each SKU
  weighted_avg_score AS (
  SELECT
    id,
    ratingCount,
    IF(total_score = 0, 3, ROUND(total_score / ratingCount, 1) ) AS averageRating -- set default score value to 3 if no review
  FROM
    rating_score )
SELECT
  *
FROM
  weighted_avg_score
